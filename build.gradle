plugins {
    id "groovy"
    id "jacoco"
    id "com.gradle.plugin-publish" version "0.10.0"
    id "java-gradle-plugin"
}

group = 'io.github.lhotari'
version = '1.3.3.1'
description = 'A Swagger to Markup (AsciiDoc and Markdown) converter Gradle Plugin. Fork of https://github.com/Swagger2Markup/swagger2markup-gradle-plugin'

tasks.withType(GroovyCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile ("io.github.swagger2markup:swagger2markup:1.3.3")
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }
    testCompile 'io.github.robwin:assertj-diff:0.1.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

test.doFirst {
    classpath = classpath.filter { !it.name.contains("snakeyaml-1.6") }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

gradlePlugin {
  plugins {
    swagger2MarkupPlugin {
      id = 'io.github.lhotari.swagger2markup'
      implementationClass = 'io.github.swagger2markup.Swagger2MarkupPlugin'
    }
  }
}

pluginBundle {
    website = 'https://github.com/lhotari/swagger2markup-gradle-plugin'
    vcsUrl = 'https://github.com/lhotari/swagger2markup-gradle-plugin'
    description = project.description
    tags = ['swagger', 'asciidoc', 'markdown']

    plugins {
        swagger2MarkupPlugin {
            id = 'io.github.lhotari.swagger2markup'
            displayName = 'Swagger2Markup Gradle plugin'
        }
    }
}
